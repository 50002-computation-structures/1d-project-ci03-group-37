module game_datapath #(
    
    SLOW_CLOCK_DIV ~ 26 : SLOW_CLOCK_DIV > 0,
    FAST_CLOCK_DIV ~ 21 : FAST_CLOCK_DIV > 0
    
)(
    input clk,  // clock
    input rst,  // reset
    
    input button0,
    input button1,
    input button2,
    input button3,
    input button4,
    
    output current_mole_index_out[3],
    output timer_out[32],
    output current_score[32]
    
) {
    
    // temporary variables
    sig input_alu_a[32]
    sig input_alu_b[32]
    
    /* instantiate alu module here */
    game_alu alu
    
    .clk(clk) {
        
        counter mole_timer_clock(#SIZE(1), #DIV(SLOW_CLOCK_DIV), .rst(rst))
        edge_detector edge_detector_mole_timer(#RISE(1), #FALL(0), .in(mole_timer_clock.value))
        
        counter rng_generator_clock(#SIZE(1), #DIV(FAST_CLOCK_DIV), .rst(rst))
        
        /* Instantiate helper module: random_number_generator */
        random_number_generator generator(.slow_clk(rng_generator_clock.value), .refresh(button0))
        
        game_cu game_cu(
            .rst(rst),
            .decrease_timer(edge_detector_mole_timer.out),
            .button0(button0),
            .button1(button1),
            .button2(button2),
            .button3(button3),
            .button4(button4)
        )
        
        regfiles regfile(
            .rst(rst),
            .we(game_cu.regfile_we),
            .wa(game_cu.regfile_wa),
            .ra_in(game_cu.regfile_ra1),
            .rb_in(game_cu.regfile_ra2)
        )
        
    }
    
    always {
        
        game_cu.regfile_rb_out = regfile.rb_out
        
        /* asel mux */
        case (game_cu.asel) {
            b00: input_alu_a = regfile.ra_out
            b11: input_alu_a = 10
            default: input_alu_a = 0
        }
        
        /* bsel mux */
        case (game_cu.bsel) {
            b00: input_alu_b = regfile.rb_out
            b01: input_alu_b = 0
            b10: input_alu_b = 1
            default: input_alu_b = 0
        }
        
        /* insert alu assignments here */
        alu.a = input_alu_a
        alu.b = input_alu_b
        alu.alufn = game_cu.alufn
        
        /* wdsel mux */
        case (game_cu.alu_out_sel) {
            b001: regfile.data = generator.out
            b010: regfile.data = 0
            b011: regfile.data = 1
            b100: regfile.data = 2
            b101: regfile.data = 3
            b110: regfile.data = 4
            default: regfile.data = alu.alu_output
        }
        
        current_mole_index_out = regfile.currentMoleIndex_out
        timer_out = regfile.timer_out
        current_score = regfile.current_score_out
        
    }
}