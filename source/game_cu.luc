module game_cu (
    input clk,  // clock
    input rst,  // reset
    input regfile_rb_out[32], // direct reading of regfile data from rb_out
    input decrease_timer,
    input button0,
    input button1,
    input button2,
    input button3,
    input button4,
    
    output alufn[6],
    output asel[3],
    output bsel[3],
    output alu_out_sel[3],
    output regfile_wa[3],
    output regfile_ra1[3],
    output regfile_ra2[3],
    output regfile_we
) {
    
    enum States {
        IDLE,
        SPAWN_MOLE,
        WAIT_INPUT,
        CHECK_HIT,
        BRANCH_CHECKHIT,
        UPDATE_SCORE,
        CHECK_MOLETIMER,
        BRANCH_MOLETIMER,
        DECREASE_MOLETIMER,
        GAMEOVER
    }
    
    dff game_fsm[$width(States)](#INIT(States.IDLE), .clk(clk), .rst(rst))
    
    always {
        
        alufn = 0
        asel = 0 
        bsel = 0
        regfile_we = 0
        regfile_wa = d6 
        regfile_ra1 = d0
        regfile_ra2 = d0
        alu_out_sel = 0
        
        game_fsm.d = game_fsm.q
        
        case (game_fsm.q) {
            
            States.IDLE: /* This state sets moleTimer to default 10s */
                alufn = b011010 // "A" operation selected!
                asel = b11 // Decimal 10 selected!
                alu_out_sel = b00 // ALU's output selected!
                regfile_we = 1 // Write Enabled!
                regfile_wa = d1 // moleTimer register selected!
                
                if (button0) {
                    game_fsm.d = States.SPAWN_MOLE
                }
            
            States.SPAWN_MOLE: /* This state writes an RNG number to currentMoleIndex */
                regfile_we = 1 // Write Enabled!
                regfile_wa = d0 // currentMoleIndex register selected!
                alu_out_sel = b001 // RNG input selected!
                game_fsm.d = States.WAIT_INPUT
            
            States.WAIT_INPUT:
                if (decrease_timer) {
                    game_fsm.d = States.CHECK_MOLETIMER
                }
                else if (button0) {
                    regfile_we = 1
                    regfile_wa = d2 // buttonPressed register selected!
                    alu_out_sel = b010 // Decimal 0 selected!
                    game_fsm.d = States.CHECK_HIT
                }
                else if (button1) {
                    regfile_we = 1
                    regfile_wa = d2 // buttonPressed register selected!
                    alu_out_sel = b011 // Decimal 1 selected!
                    game_fsm.d = States.CHECK_HIT
                }
                else if (button2) {
                    regfile_we = 1
                    regfile_wa = d2 // buttonPressed register selected!
                    alu_out_sel = b100 // Decimal 2 selected!
                    game_fsm.d = States.CHECK_HIT
                }
                else if (button3) {
                    regfile_we = 1
                    regfile_wa = d2 // buttonPressed register selected!
                    alu_out_sel = b101 // Decimal 3 selected!
                    game_fsm.d = States.CHECK_HIT
                }
                else if (button4) {
                    regfile_we = 1
                    regfile_wa = d2 // buttonPressed register selected!
                    alu_out_sel = b110 // Decimal 4 selected!
                    game_fsm.d = States.CHECK_HIT
                }
                else {
                    game_fsm.d = States.WAIT_INPUT
                }
            
            States.CHECK_HIT:
                alufn = b110011 // Compare EQUALS function selected!
                asel = b00
                bsel = b00
                regfile_ra1 = d2 // buttonPressed register selected!
                regfile_ra2 = d0 // currentMoleIndex register selected!
                alu_out_sel = b000 // ALU's output selected!
                regfile_wa = d6 // Stores result in temporary register
                regfile_we = 1 // Write Enabled!
                game_fsm.d = States.BRANCH_CHECKHIT
            
            States.BRANCH_CHECKHIT:
                regfile_ra2 = d6
                if (regfile_rb_out[0] == 1) { // if player hits the mole
                    game_fsm.d = States.UPDATE_SCORE
                } else { // if player doesn't hit the correct mole
                    game_fsm.d = States.GAMEOVER
                }
            
            States.UPDATE_SCORE:
                alufn = b000000 // Addition function selected!
                regfile_ra1 = d3 // currentScore register selected!
                asel = b00
                bsel = b10 // Decimal 1 is selected
                regfile_we = 1 // Write Enabled!
                regfile_wa = d3 // currentScore is the Write Addr
                game_fsm.d = States.SPAWN_MOLE
            
            States.CHECK_MOLETIMER:
                alufn = b110011 // Compare function selected!
                regfile_ra1 = d1 // moleTimer register selected!
                asel = b00
                bsel = b01 // Decimal 0 is selected!
                regfile_we = 1 // Write Enabled!
                regfile_wa = d6 // Stores result in temporary register (to be read next cycle)
                game_fsm.d = States.BRANCH_MOLETIMER
            
            States.BRANCH_MOLETIMER:
                regfile_ra2 = d6 // Read temporary register (contains the Compare output)
                if (~regfile_rb_out[0]) { // if timer is not zero
                    game_fsm.d = States.DECREASE_MOLETIMER
                } else {
                    game_fsm.d = States.GAMEOVER
                }
            
            States.DECREASE_MOLETIMER:
                alufn = b000001 // Subtraction function selected!
                regfile_ra1 = d1 // moleTimer register selected!
                asel = b00
                bsel = b10 // Decimal 1 is selected
                regfile_we = 1 // Write Enabled!
                regfile_wa = d1 // moleTimer is the Write Addr
                game_fsm.d = States.WAIT_INPUT
            
            States.GAMEOVER:
                game_fsm.d = States.GAMEOVER
            
        }
        
    }
}